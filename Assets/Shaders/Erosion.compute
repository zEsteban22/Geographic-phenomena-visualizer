// List of all separate kernels to be dispatched from CustomTerrain.cs
#pragma kernel RainAndControl
#pragma kernel FluxComputation
#pragma kernel FluxApply
#pragma kernel HydraulicErosion
#pragma kernel SedimentAdvection
#pragma kernel ThermalErosion
#pragma kernel ApplyThermalErosion

// Number of threads in each kernel
#define NUMTHREADS numthreads(8, 8, 1)

// Directional maps channel semantics
// It means that for textures with 4-directional data (like flux)
// value for the each direction stored in a separate channel
// For example, LEFT is stored in RED channel
#define LDIR(col) (col.r)
#define RDIR(col) (col.g)
#define TDIR(col) (col.b)
#define BDIR(col) (col.a)

// State channel semantics
#define TERRAIN_HEIGHT(col) (col.r)
#define WATER_HEIGHT(col) (col.g)
#define SEDIMENT(col) (col.b)
#define HARDNESS(col) (col.a)
#define FULL_HEIGHT(col) ((TERRAIN_HEIGHT(col) + WATER_HEIGHT(col)))

// Velocity-map channel semantics
#define VELOCITY_X(col) (col.r)
#define VELOCITY_Y(col) (col.g)

// Sampling macro, just to simplify the code a bit
#define SAMPLE_WITH_OFFSET(tex, x, y) ((tex[id.xy + uint2(x, y)]))
#define CURRENT_SAMPLE(tex) (tex[id.xy])
#define LEFT_SAMPLE(tex) (SAMPLE_WITH_OFFSET(tex, -1, 0))
#define RIGHT_SAMPLE(tex) (SAMPLE_WITH_OFFSET(tex, 1, 0))
#define TOP_SAMPLE(tex) (SAMPLE_WITH_OFFSET(tex, 0, 1))
#define BOTTOM_SAMPLE(tex) (SAMPLE_WITH_OFFSET(tex, 0, -1))

#define SUM_COMPS(v) ((v.r + v.g + v.b + v.a))


/* ======== INPUTS ======== */

// Heightmap (R - height, G - water height, B - suspended sediment amount, R - Surface hardness)
RWTexture2D<float4> HeightMap;

// Velocity field (R - velocity x, G - velocity y)
RWTexture2D<float2> VelocityMap;

// Water Flux field (R - left, G - right, B - top, A - bottom)
RWTexture2D<float4> FluxMap;

// Terrain Flux field (R - left, G - right, B - top, A - bottom)
RWTexture2D<float4> TerrainFluxMap;

uint _InputMode;
float4 _InputControls;

// Fluid simulation properties
uint _Width;
uint _Height;
float _TimeDelta;
float _PipeArea;
float _Gravity;
float _PipeLength;
float2 _CellSize;
float _Evaporation;
float _RainRate;

// Hydraulic erosion properties
float _SedimentCapacity;
float _MaxErosionDepth;
float _SuspensionRate;
float _DepositionRate; 
float _SedimentSofteningRate;

// Thermal erosion properties
float _ThermalErosionTimeScale;
float _ThermalErosionRate;
float _TalusAngleTangentCoeff;
float _TalusAngleTangentBias;

float4 SampleBilinear(RWTexture2D<float4> tex, float2 uv)
{
	float2 uva = floor(uv);
	float2 uvb = ceil(uv);

	uint2 id00 = (uint2)uva;  // 0 0
	uint2 id10 = uint2(uvb.x, uva.y); // 1 0
	uint2 id01 = uint2(uva.x, uvb.y); // 0 1	
	uint2 id11 = (uint2)uvb; // 1 1

	float2 d = uv - uva;

	return
		tex[id00] * (1 - d.x) * (1 - d.y) +
		tex[id10] * d.x * (1 - d.y) +
		tex[id01] * (1 - d.x) * d.y +
		tex[id11] * d.x * d.y;
}

/* Pass 1. Rain simulation and brush drawing */
[NUMTHREADS]
void RainAndControl(uint3 id : SV_DispatchThreadID) 
{	
	float4 currentState = CURRENT_SAMPLE(HeightMap);	
	WATER_HEIGHT(currentState) += _TimeDelta * _RainRate;

	// Water-drawing brush
	float2 uv = float2((float)id.x / _Width, (float)id.y / _Height);
	float2 dist = uv - _InputControls.xy;
	if (length(dist) < abs(_InputControls.z)) 
	{
		if(_InputMode == 0)
			WATER_HEIGHT(currentState) = max(0, WATER_HEIGHT(currentState) + _InputControls.w * _TimeDelta);
		else if (_InputMode == 1)
			WATER_HEIGHT(currentState) = max(0, WATER_HEIGHT(currentState) - _InputControls.w * _TimeDelta);
		else if (_InputMode == 2)
			TERRAIN_HEIGHT(currentState) = max(0, TERRAIN_HEIGHT(currentState) + _InputControls.w * _TimeDelta);
		else if (_InputMode == 3)
			TERRAIN_HEIGHT(currentState) = max(0, TERRAIN_HEIGHT(currentState) - _InputControls.w * _TimeDelta);
	}

	// Write new state to the HeightMap
	CURRENT_SAMPLE(HeightMap) = currentState;
}


/* Pass 2. Flux field computation */
[NUMTHREADS]
void FluxComputation(uint3 id : SV_DispatchThreadID) 
{
	// Sample the heighmap (state map)
	float4 state = CURRENT_SAMPLE(HeightMap);
	float4 stateLeft = LEFT_SAMPLE(HeightMap);
	float4 stateRight = RIGHT_SAMPLE(HeightMap);
	float4 stateTop = TOP_SAMPLE(HeightMap);
	float4 stateBottom = BOTTOM_SAMPLE(HeightMap);

	float terrainHeight = TERRAIN_HEIGHT(state);
	float waterHeight = WATER_HEIGHT(state);

	// Flow simulation using shallow-water model. Computation of the velocity field and water height changes.
	// Sample flux
	float4 outputFlux = CURRENT_SAMPLE(FluxMap);

	// Overall height difference in each direction
	float4 heightDifference = FULL_HEIGHT(state) - float4(
		FULL_HEIGHT(stateLeft),
		FULL_HEIGHT(stateRight),
		FULL_HEIGHT(stateTop),
		FULL_HEIGHT(stateBottom));

	// Output flux	
	outputFlux = max(0, outputFlux + _TimeDelta * _Gravity * _PipeArea * heightDifference / _PipeLength);

	/*
		Rescale flux
		The total outflow should not exceed the total amount
		of the water in the given cell.If the calculated value is
		larger than the current amount in the given cell, then flux will
		be scaled down with an appropriate factor
	*/
	outputFlux *= min(1, waterHeight * _CellSize.x * _CellSize.y / (SUM_COMPS(outputFlux) * _TimeDelta));

	// Boundaries (uncomment thisif you want water to bounce of boundaries)						
	if (id.x == 0) LDIR(outputFlux) = 0;
	if (id.y == 0) BDIR(outputFlux) = 0;
	if (id.x == _Width - 1) RDIR(outputFlux) = 0;
	if (id.y == _Height - 1) TDIR(outputFlux) = 0;	

	// Write new flux to the FluxMap
	CURRENT_SAMPLE(FluxMap) = max(0, outputFlux);
}


/* Pass 3. Applying fluxes to the actual cells and changing weter height */
[NUMTHREADS]
void FluxApply(uint3 id : SV_DispatchThreadID)
{
	float4 state = CURRENT_SAMPLE(HeightMap);
	float4 outputFlux = CURRENT_SAMPLE(FluxMap);
	float4 inputFlux = float4(
		RDIR(LEFT_SAMPLE(FluxMap)),
		LDIR(RIGHT_SAMPLE(FluxMap)),
		BDIR(TOP_SAMPLE(FluxMap)),
		TDIR(BOTTOM_SAMPLE(FluxMap)));
	float waterHeightBefore = WATER_HEIGHT(state);

	// Water surface and velocity field update
	// volume is changing by amount on incoming fluid volume minus outgoing
	float volumeDelta = SUM_COMPS(inputFlux) - SUM_COMPS(outputFlux);	

	// Then, we update the water height in the current (x, y) cell:
	WATER_HEIGHT(state) += _TimeDelta * volumeDelta / (_CellSize.x * _CellSize.y);	

	// Write new state to the HeightMap
	CURRENT_SAMPLE(HeightMap) = state;

	// Compute new velocity from flux to the VelocityMap
	CURRENT_SAMPLE(VelocityMap) = float2(
		0.5 * (LDIR(inputFlux) - LDIR(outputFlux) + RDIR(outputFlux) - RDIR(inputFlux)),
		0.5 * (BDIR(inputFlux) - BDIR(outputFlux) + TDIR(outputFlux) - TDIR(inputFlux)));
		/// _PipeLength * 0.5 * (waterHeightBefore + WATER_HEIGHT(state));
}


/* Pass 4. Terrain hydraulic erosion and deposition */
[NUMTHREADS]
void HydraulicErosion(uint3 id : SV_DispatchThreadID)
{	
	// Sample the heighmap (state map)
	float4 state = CURRENT_SAMPLE(HeightMap);
	float4 stateLeft = LEFT_SAMPLE(HeightMap);
	float4 stateRight = RIGHT_SAMPLE(HeightMap);
	float4 stateTop = TOP_SAMPLE(HeightMap);
	float4 stateBottom = BOTTOM_SAMPLE(HeightMap);
	float2 velocity = CURRENT_SAMPLE(VelocityMap);


	// Tilt angle computation
	float3 dhdx = float3(2 * _CellSize.x, TERRAIN_HEIGHT(stateRight) - TERRAIN_HEIGHT(stateLeft), 0);
	float3 dhdy = float3(0, TERRAIN_HEIGHT(stateTop) - TERRAIN_HEIGHT(stateBottom), 2 * _CellSize.y);
	float3 normal = cross(dhdx, dhdy);

	float sinTiltAngle = abs(normal.y) / length(normal);
	
	// Erosion limiting factor
	float lmax = saturate(1 - max(0, _MaxErosionDepth - WATER_HEIGHT(state)) / _MaxErosionDepth);
	float sedimentTransportCapacity = _SedimentCapacity * length(velocity) * min(sinTiltAngle, 0.05) * lmax;

	if (SEDIMENT(state) < sedimentTransportCapacity)
	{
		float mod = _TimeDelta * _SuspensionRate * HARDNESS(state) * (sedimentTransportCapacity - SEDIMENT(state));		
		TERRAIN_HEIGHT(state) -= mod;
		SEDIMENT(state) += mod;
		WATER_HEIGHT(state) += mod;
	}
	else
	{
		float mod = _TimeDelta * _DepositionRate * (SEDIMENT(state) - sedimentTransportCapacity);
		TERRAIN_HEIGHT(state) += mod;
		SEDIMENT(state) -= mod;
		WATER_HEIGHT(state) -= mod;
	}	

	// Water evaporation.
	WATER_HEIGHT(state) *= 1 - _Evaporation * _TimeDelta;
	 
	// Hardness update
	HARDNESS(state) = HARDNESS(state) - _TimeDelta * _SedimentSofteningRate * _SuspensionRate * (SEDIMENT(state) - sedimentTransportCapacity);
	HARDNESS(state) = clamp(HARDNESS(state), 0.1, 1);

	// Write heighmap
	CURRENT_SAMPLE(HeightMap) = state;
}


/* Pass 5. Terrain hydraulic erosion second step. Advecting sediment */
[NUMTHREADS]
void SedimentAdvection(uint3 id : SV_DispatchThreadID)
{
	float4 state = CURRENT_SAMPLE(HeightMap);
	float2 velocity = CURRENT_SAMPLE(VelocityMap); 

	// Sediment advection
	SEDIMENT(state) = SEDIMENT(SampleBilinear(HeightMap, id.xy - velocity * _TimeDelta));

	// Write heightmap
	CURRENT_SAMPLE(HeightMap) = state;
}


/* Pass 6. Terrain thermal erosion */
[NUMTHREADS]
void ThermalErosion(uint3 id : SV_DispatchThreadID)
{	
	float4 state = CURRENT_SAMPLE(HeightMap);	

	// Neighbors
	float4 neighborHeights = float4(
		TERRAIN_HEIGHT(LEFT_SAMPLE(HeightMap)),
		TERRAIN_HEIGHT(RIGHT_SAMPLE(HeightMap)),
		TERRAIN_HEIGHT(TOP_SAMPLE(HeightMap)),
		TERRAIN_HEIGHT(BOTTOM_SAMPLE(HeightMap))
	);

	// Overall height difference in each direction
	float4 heightDifference = max(0, TERRAIN_HEIGHT(state) - neighborHeights);
	float maxHeightDifference = max(max(heightDifference.x, heightDifference.y), max(heightDifference.z, heightDifference.w));

	// First, we need to compute the amount of terrain to be moved from the current cell
	// It is capped at [Area * MaxHeightDifference / 2] because it will oscillate if we will allow 
	// more mass to flow per update
	// ErosionRate and Hardness are just control variables to reduce the erosion where and when needed
	float volumeToBeMoved = _CellSize.x * _CellSize.y * maxHeightDifference * 0.5 
		* _ThermalErosionRate * HARDNESS(state);
	
	// Compute angles for every neighbor
	// Actually a tan(angle)
	// NOTE: If Cellsize.X and _Cellsize.y are different 
	// you need to use .x for first 2 components and .y for last 2
	float4 tanAngle = heightDifference / _CellSize.x;
	
	// We need to define a threshold for the angle to identify in which direction the mass is falling
	// It based on hardness of the material and some more control variables
	float treshold = HARDNESS(state) * _TalusAngleTangentCoeff + _TalusAngleTangentBias;
	
	// Next we need to set proportions that defines how much mass is transfered in each direction
	// Some directions will not contribute because of not enough steep angles
	// We are 
	float4 k = 0;
	
	if (tanAngle.x > treshold)
		k.x = heightDifference.x;

	if (tanAngle.y > treshold)
		k.y = heightDifference.y;

	if (tanAngle.z > treshold)
		k.z = heightDifference.z;

	if (tanAngle.w > treshold)
		k.w = heightDifference.w;	

	// Output flux
	float sumProportions = SUM_COMPS(k);
	float4 outputFlux = 0;

	if (sumProportions > 0)
		outputFlux = volumeToBeMoved * k / sumProportions;
		
	// Boundaries (uncomment thisif you want water to bounce of boundaries)						
	if (id.x == 0) LDIR(outputFlux) = 0;
	if (id.y == 0) BDIR(outputFlux) = 0;
	if (id.x == _Width - 1) RDIR(outputFlux) = 0;
	if (id.y == _Height - 1) TDIR(outputFlux) = 0;	

	CURRENT_SAMPLE(TerrainFluxMap) = outputFlux;
}

/* Pass 7. Apply thermal erosion */
[NUMTHREADS]
void ApplyThermalErosion(uint3 id : SV_DispatchThreadID)
{
	float4 state = CURRENT_SAMPLE(HeightMap);
	float4 outputFlux = CURRENT_SAMPLE(TerrainFluxMap);
	float4 inputFlux = float4(
		RDIR(LEFT_SAMPLE(TerrainFluxMap)),
		LDIR(RIGHT_SAMPLE(TerrainFluxMap)),
		BDIR(TOP_SAMPLE(TerrainFluxMap)),
		TDIR(BOTTOM_SAMPLE(TerrainFluxMap)));	
	
	// Volume is changing by amount on incoming mass minus outgoing mass
	float volumeDelta = SUM_COMPS(inputFlux) - SUM_COMPS(outputFlux);

	// Then, we update the terrain height in the current (x, y) cell
	// min - is to prevent addind more mass than in flux
	TERRAIN_HEIGHT(state) += min(1, _TimeDelta * _ThermalErosionTimeScale) * volumeDelta;

	// Write new state to the HeightMap
	CURRENT_SAMPLE(HeightMap) = state;
}